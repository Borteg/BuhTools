#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ДанныеПосредников.Записывать = Истина;

	Для Каждого СтрокаСДанными Из ДанныеПосредников Цикл
		ДвиженияПоРегистрам = Движения.ДанныеПосредников.Добавить();
		ДвиженияПоРегистрам.Период = НачалоМесяца(МесяцДанных);
		ЗаполнитьЗначенияСвойств(ДвиженияПоРегистрам, СтрокаСДанными);
	КонецЦикла;
	
КонецПроцедуры


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(МесяцДанных) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не указан месяц";
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
	   ЗаполнитьДаннымиПоТаблицеДанных(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДаннымиПоТаблицеДанных(ДанныеДляЗаполнения)
	
	Месяц = Неопределено;
	Данные = Неопределено;

	Если ДанныеДляЗаполнения.Свойство("Месяц", Месяц) И ДанныеДляЗаполнения.Свойство("Данные", Данные) Тогда
		ДанныеПосредников.Загрузить(Данные);
		МесяцДанных = НачалоМесяца(Месяц);
	КонецЕсли;


КонецПроцедуры

#КонецЕсли